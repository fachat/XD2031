

;telnet8 ==0401==
    0 printchr$(14)
    1 open1,8,15,"a2:tcp:localhost"
    2 input#1,a$,b$,c$,d$
    3 printa$","b$","c$","d$
    4 close1
    9 rem non-blocking, read-write open
   10 open1,8,2,"2:telnet,n,x"
   20 gosub 1000:as=st:if a<>0 then print chr$(a);
   21 a$="":geta$:gosub 3000
   25 rem printst
   27 rem only if st=64 we are sure there is no timeout
   28 rem and status is correct. as is saved status from
   29 rem above
   30 if as = 64 then 40
   35 goto 20
   40 close1
  999 end
 1000 rem ************************
 1010 rem get byte from net and check for option
 1015 a$="":a=0
 1017 rem iec and ieee are different in st behaviour!
 1020 get#1,a$:if (st and 66) or a$="" then return
 1030 a=asc(a$)
 1040 if a=255 goto 1100:rem tel_iac
 1045 rem ascii to petscii
 1050 if a<65 then return
 1055 if a<91 then a=a+128:return
 1060 if a<97 then return
 1065 if a<123then a=a-32:return
 1070 return
 1100 rem ************************
 1110 rem control code
 1120 get#1,a$:if (st and 2) or a$="" then 1120
 1130 a=asc(a$)
 1140 if a<251 then a=0:return
 1145 if a>254 then a=0:return
 1200 rem option value
 1210 get#1,a$:if (st and 2) or a$="" then 1210
 1220 b=asc(a$)
 1230 if b=1 then 1800:rem echo
 1240 if b=3 then 1900:rem ga
 1290 print"option:";b;" value ";a
 1300 rem default responses
 1310 if a=251 then 1400:rem will -> dont reply
 1320 if a=252 then 1400:rem wont -> dont reply
 1330 if a=253 then 1500:rem do -> wont reply
 1340 if a=254 then 1500:rem dont -> wont reply
 1350 a=0:return:rem should not happen
 1400 rem dont reply
 1410 print#1,chr$(255);chr$(254);chr$(b);
 1420 a=0:return
 1500 rem wont reply
 1510 print#1,chr$(255);chr$(252);chr$(b);
 1520 a=0:return
 1800 rem echo option
 1810 print"echo: ";: gosub 2000:print
 1820 goto 1300:rem wont
 1900 rem suppress go-ahead function
 1910 print "suppress ga: ";:gosub 2000:print
 1915 if a<>251 then 1300
 1920 print#1,chr$(255);chr$(253);chr$(3);
 1930 a=0:return
 2000 rem print option
 2010 if a=251 then print" will ";:return
 2020 if a=252 then print" wont ";:return
 2030 if a=253 then print" do ";:return
 2040 if a=254 then print" dont ";:return
 2050 print " ??? ";:return
 3000 rem **********************
 3010 rem send char in a$ to net
 3020 if len(a$) = 0 then return
 3030 a=asc(a$)
 3040 gosub 4000
 3050 print#1,chr$(a);
 3060 return
 4000 rem petscii to ascii
 4010 if a < 65 then return
 4020 if a < 91 then a=a+32:return
 4025 if a < 97 then return
 4030 if a < 123 then a=a-32:return
 4035 if a < 193 then return
 4040 if a < 219 then a=a and 127:return
 4045 return

